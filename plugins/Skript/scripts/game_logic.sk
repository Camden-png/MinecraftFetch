# === Game Logic ===

import:
    com.camden.skriptutils.Utils

options:
    green: "light green"

function check_has_bone(player: player):
    set {_has_bone} to false
    loop all items in {_player}'s inventory:
        if loop-item is bone:
            set {_has_bone} to true
            stop loop

    set {_old_status} to get_key({_player}, "has_bone")
    if {_old_status} is {_has_bone}:
        stop

    set_key({_player}, "has_bone", {_has_bone})

    # Apply / remove glow
    # TODO: switch to new glow logic...
    if {_has_bone} is true:
        apply_glow({_player})
    else:
        remove_glow({_player})

# Check if player has the bone...
every 5 ticks:
    loop all players:
        if in_game(loop-player, true) is true:  # Strict...
            check_has_bone(loop-player)

        # Passively update scoreboard
        if loop-player is op:
            set_scoreboard(loop-player)

on tool change:
    if in_lobby(player) is false:
        stop

    set {_old_status} to get_key(player, "is_ready")

    set {_curr_status} to false
    if tool is lime banner:
        set {_curr_status} to true

    if {_old_status} is {_curr_status}:
        stop

    set_key(player, "is_ready", {_curr_status})

    set {_level_name} to get_key(player, "level_name")
    _update_level_data({_level_name})

on rightclick holding an ender pearl:
    if in_lobby(player) is true:  # ONLY allow in lobby...
        _leave(player)
        cancel event

command /spawn:
    trigger:
        if in_game(player) is true:
            _leave(player)
            stop
        
        set {_message} to format_message("TP", "Teleported...", {@green})
        send {_message} to player
        tp_to_spawn(player)

# === Edge-cases ===

# Item drop, item moves, etc.
on inventory click:
    if in_lobby(player) is true:
        cancel event

on drop:
    if in_lobby(player) is true:
        cancel event

on swap hand item:
    if in_lobby(player) is true:
        cancel event

on command "/clear":
    if player is not op:
        stop
    if in_game(player) is true:
        _leave(player)
        cancel event

on quit:
    if in_game(player) is true:
        _leave(player)
